let recursive = \self ->
    (\a -> self (\b -> (a a) b))
    (\a -> self (\b -> (a a) b))

let :> = \a -> \b -> :! (:< a b)
let :<= = \a -> \b -> or (:< a b) (:= a b)
let :>= = \a -> \b -> or (:> a b) (:= a b)

let true = 1
let false = 0
let _ = nil

let pair = \first -> \second -> \consumer -> consumer first second

let head = \pair -> pair (\head -> \tail -> head)
let tail = \pair -> pair (\head -> \tail -> tail)

let map = recursive (\self ->
    \transform -> \sequence ->
        if := (tail sequence) nil
        then pair (transform (head sequence)) nil
        else pair
            (transform (head sequence))
            (self transform (tail sequence))
)

let fold = recursive (\self ->
    \accumulate -> \value -> \sequence ->
        if := (tail sequence) nil
        then accumulate value (head sequence)
        else self (accumulate) (accumulate value (head sequence)) (tail sequence) 
)
